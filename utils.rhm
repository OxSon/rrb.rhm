#lang rhombus

export:
  TODO
  filter
  ^

macro 'TODO $(msg :: String)':
  'error("TODO: " +& $msg)'

macro 'assert($predicate)':
    'if $predicate
     | values()
     | error("assertion failed: " +& '$predicate')'

fun filter(predicate :~ Function.of_arity(1), lst :~ List):
  match lst
  | []: []
  // it would be nice if there was a more ergonomic pattern to use here
  | List.cons(fst, rst):
      if predicate(fst)
      | List.cons(fst, filter(predicate, rst))
      | filter(predicate, rst)


operator base ^ exp:
  ~stronger_than: * / + - <= >= == >> << and
  ~associativity: ~right
                  
  math.expt(base, exp)


module test:
  check: TODO "foo"
         ~raises "foo"

  check: assert(1 == 2)
         ~raises "assertion failed: " +& '1 == 2'

  check: assert((fun (x): x == 2)(1))
         ~raises "assertion failed: " +& '(fun (x): x == 2)(1)'

  check: assert(9 mod 3 == 1)
         ~raises "assertion failed: 9 mod 3 == 1"

  check: assert(filter(fun (x): x mod 2 == 0, [2, 4, 6, 8]))
         ~completes

  check: filter(fun(_): #true, [1, 2, 3])
         ~is [1, 2, 3]

  check: filter(fun(n): n mod 2 == 0, [1, 2, 3, 4, 5, 6])
         ~is [2, 4, 6]